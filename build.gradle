// First, apply the publishing plugin
buildscript {
    repositories {
        mavenLocal()        // for e2e test
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.shipkit:shipkit:0.9.1"
        classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.3.0'
    }
}

apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'checkstyle'
apply plugin: 'codenarc'

apply from: 'gradle/java6-compatibility.gradle'
apply from: 'gradle/precommit.gradle'

apply plugin: 'org.shipkit.gradle-plugin'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
}

group = 'org.shipkit'

dependencies {
    compile gradleApi()

    compile "com.github.cliftonlabs:json-simple:2.1.2"
    compile 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    compile "com.gradle.publish:plugin-publish-plugin:0.9.6"
    compile 'com.googlecode.java-diff-utils:diffutils:1.3.0'

    testCompile("org.spockframework:spock-core:1.1-groovy-2.4") {
        exclude module: "groovy-all"
    }

    testCompile "net.bytebuddy:byte-buddy:1.7.1"
    testCompile "org.objenesis:objenesis:2.5"
    testCompile "commons-lang:commons-lang:2.4"
    testCompile gradleTestKit()
}

pluginBundle {
    website = 'http://shipkit.org/'
    vcsUrl = 'https://github.com/mockito/shipkit'
    description = 'Shipkit - toolkit for shipping it. Enables continuous delivery with automated version bumps, release notes and many more.'
    tags = ['shipkit', 'continuous delivery', 'release']

    mavenCoordinates {
        groupId = 'org.shipkit'
        artifactId = 'shipkit'
    }
}

//remove doclint warnings that pollute javadoc logs when building with java8
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task fastInstall { Task t ->
    description = "Fast installation for quick local testing."
    t.dependsOn install
    gradle.taskGraph.whenReady {
        if (it.hasTask(t)) {
            //we don't need them for local testing so lets disable for faster execution
            tasks.javadoc.enabled = false
            tasks.groovydoc.enabled = false
        }
    }
    t.doLast { println "  Shipkit $version installed to local maven repo" }
}
