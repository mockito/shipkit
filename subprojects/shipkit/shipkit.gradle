apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'checkstyle'
apply plugin: 'codenarc'
apply plugin: 'jacoco'

apply plugin: 'com.gradle.plugin-publish'

apply from: "$rootDir/gradle/upgrade-downstream.gradle"

checkstyle {
    toolVersion = '8.19'
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile gradleApi()

    compile "com.github.cliftonlabs:json-simple:2.1.2"
    compile 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.3'
    compile "com.gradle.publish:plugin-publish-plugin:0.9.10"
    compile 'com.googlecode.java-diff-utils:diffutils:1.3.0'
    compile 'com.github.technoir42:aar-publish-plugin:1.0.2'

    testCompile("org.spockframework:spock-core:1.3-groovy-2.4") {
        exclude module: "groovy-all"
    }
    testCompile 'info.solidsoft.spock:spock-global-unroll:0.5.1'    //automatically unroll Spock tests (if appropriate)
    testCompile "net.bytebuddy:byte-buddy:1.9.10"
    testCompile "org.objenesis:objenesis:3.0.1"
    testCompile gradleTestKit()
}

test {
    testLogging {
        info.events = ["failed", "skipped"]
    }
    afterSuite { desc, result ->
        if (!desc.parent)
            logger.info("Test summary: " +
                "${result.testCount} tests " +
                "(${result.successfulTestCount} succeeded, " +
                "${result.failedTestCount} failed, " +
                "${result.skippedTestCount} skipped)")
    }
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
}

codenarc {
    toolVersion = '1.2.1'
}

pluginBundle {
    website = 'http://shipkit.org/'
    vcsUrl = 'https://github.com/mockito/shipkit'
    description = 'Shipkit - toolkit for shipping it. Enables continuous delivery with automated version bumps, release notes and many more.'
    tags = ['shipkit', 'continuous delivery', 'release']

    mavenCoordinates {
        groupId = 'org.shipkit'
        artifactId = 'shipkit'
    }
}

//remove doclint warnings that pollute javadoc logs when building with java8
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task fastInstall { Task t ->
    description = "Fast installation for quick local testing."
    t.dependsOn install
    gradle.taskGraph.whenReady {
        if (it.hasTask(t)) {
            //we don't need them for local testing so lets disable for faster execution
            tasks.javadoc.enabled = false
            tasks.groovydoc.enabled = false
        }
    }
    t.doLast { println "  Shipkit $version installed to local maven repo" }
}

jacoco {
    toolVersion = "0.8.3"
}

task codeCoverageReport(type:JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    sourceSets sourceSets.main

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn test
check.dependsOn codeCoverageReport
